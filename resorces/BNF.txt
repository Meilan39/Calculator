<command> ::= <nonvariable>
            | <zeros>

<nonvariable> ::= <expression>

<zeros> ::= zeros <polynomial>

/**************************** zeros *******************************/

<polynomial> ::= <polynomial_term> {<polynomial_suffix}*
<polynomial_suffix> ::= + <polynomial_term>
                      | - <polynomial_term>
<polynomial_term> ::= {<sign>}? {<real_number>}? x ^ <number>
                    | {<sign>}? {<real_number>}? x
                    | {<sign>}? <real_number>

/**************************** expression *******************************/

<expression> ::= <additive_expression>

<additive_expression> ::= <multiplicative_expression> {<additive_expression_suffix>}*
<additive_expression_suffix> ::=  + <multiplicative_expression>
                               |  - <multiplicative_expression>

<multiplicative_expression> ::= {<sign>}? <exponential_expression> {<multiplicative_expression_suffix>}*
<multiplicative_expression_suffix> ::=  * <exponential_expression>
                                     |  / <exponential_expression>
                                     | <exponential_expression>

<exponential_expression> ::= <primary_expression> {<exponential_expression_suffix>}*
<exponential_expression_suffix> ::= ^ <primary_expression>

<primary_expression> ::= <real_number>
                       | <functions>
                       | <variable>
                       | ( <expression> )
                       | { <expression> }

/******************************** functions **************************************/

<functions> ::= <root> | <sqrt> | <log> | <sqrt> | <log> | <ln> | <sin> | <cos> | <tan> 
              | <asin> | <acos> | <atan> | <sinh> | <cosh> | <tanh>

<root> ::= root ( <number> , <expression> )

<sqrt> ::= sqrt <primary_expression>

<log> ::= log ( <number> , <expression> )

<ln> ::= ln <primary_expression>

<sin> ::= sin <primary_expression>

<cos> ::= cos <primary_expression>

<tan> ::= tan <primary_expression>

<asin> ::= asin <primary_expression>

<acos> ::= acos <primary_expression>

<atan> ::= atan <primary_expression>

<sinh> ::= sinh <primary_expression>

<cosh> ::= cosh <primary_expression>

<tanh> ::= tanh <primary_expression>

/******************************** general **************************************/

<real_number> ::= <rational> | <irrational>

<rational> ::= <scientific> | <decimal> | <number> | <zero>

<irrational> ::= e | pi

<scientific> ::= <decimal> E {<sign>}? <number>
               | <number> E {<sign>}? <number>

<sign> ::= + | -

<variable> ::= x