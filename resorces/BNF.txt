<command> ::= <nonvariable>
            | <zeros>

<nonvariable> ::= <expression>

<zeros> ::= zeros <polynomial>

/**************************** zeros *******************************/

<polynomial> ::= <polynomial_term> {<polynomial_suffix}*
<polynomial_suffix> ::= + <polynomial_term>
                      | - <polynomial_term>
<polynomial_term> ::= {<real_number>}? x ^ <natural>
                    | {<real_number>}? x
                    | <real_number>

/**************************** expression *******************************/

<expression> ::= <additive_expression>

<additive_expression> ::= <multiplicative_expression> {<additive_expression_suffix>}*
<additive_expression_suffix> ::=  + <multiplicative_expression>
                               |  - <multiplicative_expression>

<multiplicative_expression> ::= <exponential_expression> {<multiplicative_expression_suffix>}*
<multiplicative_expression_suffix> ::=  * <exponential_expression>
                                     |  / <exponential_expression>

<exponential_expression> ::= <parenthetical_expression> {<exponential_expression_suffix>}*
<exponential_expression_suffix> ::= ^ <parenthetical_expression>

<parenthetical_expression> ::= <functions>
                             | <compound_number>
                             | ( <expression> )
                             | { <expression> }

/******************************** functions **************************************/

<functions> ::= <root> | <sqrt> | <log> | <sqrt> | <log> | <ln> | <sin> | <cos> | <tan> 
              | <asin> | <acos> | <atan> | <sinh> | <cosh> | <tanh>

<root> ::= root ( <natural> ) ( <expression> )

<sqrt> ::= sqrt ( <expression> )

<log> ::= log ( <natural> ) ( <expression> )

<ln> ::= ln ( <expression> )

<sin> ::= sin ( <expression> )

<cos> ::= cos ( <expression> )

<tan> ::= tan ( <expression> )

<asin> ::= asin ( <expression> )

<acos> ::= acos ( <expression> )

<atan> ::= atan ( <expression> )

<sinh> ::= sinh ( <expression> )

<cosh> ::= cosh ( <expression> )

<tanh> ::= tanh ( <expression> )

/******************************** general **************************************/

<compound_number> ::= <real_number> <variable>
                    | <real_number>
                    | <variable>

<real_number> ::= <irrational>
                | <scientific>
                | <rational>

<irrational> ::= {<scientific>}? <special_symbols>
               | {<rational>}? <special_symbols>
               | {<sign>}? <special_symbols>

<scientific> ::= <rational> E <integer>

<natural> ::= <number>
<integer> ::= {<sign>}? <number>
            | <zero>
<rational> ::= {<sign>}? <decimal>
             | {<sign>}? <number>
             | <zero>

<special_symbols> ::= e | pi

<sign> ::= + | -

<variable> ::= x